// Copyright 2018-2020 Twitter, Inc.
// Licensed under the MoPub SDK License Agreement
// http://www.mopub.com/legal/sdk-license-agreement/
apply from: 'shared-build.gradle'
import mobi.idealabs.ads.gradle.AdsPlugin
import mobi.idealabs.fataar.FatLibraryPlugin

apply plugin: FatLibraryPlugin
apply plugin: AdsPlugin
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    embed project(path: ':mopub-sdk:mopub-sdk-base', configuration: 'default')
    embed project(path: ':mopub-sdk:mopub-sdk-banner', configuration: 'default')
    embed project(path: ':mopub-sdk:mopub-sdk-interstitial', configuration: 'default')
    embed project(path: ':mopub-sdk:mopub-sdk-rewardedvideo', configuration: 'default')
    embed project(path: ':mopub-sdk:mopub-sdk-native-static', configuration: 'default')
    embed project(path: ':mopub-sdk:mopub-sdk-native-video', configuration: 'default')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.robolectric:shadows-httpclient:4.3'
    testImplementation 'com.squareup:fest-android:1.0.7'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.apache.commons:commons-lang3:3.9'

    ['powermock-api-mockito',
     'powermock-core',
     'powermock-module-junit4',
     'powermock-module-junit4-rule',
     'powermock-classloading-xstream'].each {
        testImplementation ("org.powermock:${it}:1.7.4") {
            exclude group: 'org.mockito', module: 'mockito-core'
        }
    }
}

android {
    useLibrary 'org.apache.http.legacy'

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        // separate block is necessary for Robolectric properties.
        unitTests.all {
            def sdkVersionsToTest = "${System.env.ROBOELECTRIC_TEST_API_VERSIONS}"

            if (sdkVersionsToTest
                    && !sdkVersionsToTest.allWhitespace
                    && sdkVersionsToTest.toString() != "null") {
                systemProperty 'robolectric.enabledSdks', sdkVersionsToTest
            }
        }
    }
}

project.description = 'MoPub Android SDK'
project.ext.displayName = 'MoPub Android SDK'
project.ext.projectName = 'mopub-sdk'
project.ext.uploadName = 'mopub-android-sdk'
apply from: 'publisher.gradle'
